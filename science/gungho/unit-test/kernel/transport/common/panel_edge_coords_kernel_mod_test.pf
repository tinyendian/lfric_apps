!-----------------------------------------------------------------------------
! (C) Crown copyright 2025 Met Office. All rights reserved.
! The file LICENCE, distributed with this code, contains details of the terms
! under which the code may be used.
!-----------------------------------------------------------------------------

!> Test the computation of the cubed-sphere panel extension
module panel_edge_coords_kernel_mod_test

  use constants_mod, only: i_def, r_def, pi, IMDI
  use funit

  implicit none

  private
  public :: test_all

  @TestCase
  type, extends(TestCase), public :: panel_edge_coords_test_type
    private
  contains
    procedure setUp
    procedure tearDown
    procedure test_all
  end type panel_edge_coords_test_type

contains

  !!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
  subroutine setUp( this )

    use base_mesh_config_mod,      only : geometry_spherical,                  &
                                          topology_fully_periodic
    use sci_chi_transform_mod,     only : init_chi_transforms
    use extrusion_config_mod,      only : method_uniform,                      &
                                          stretching_method_linear
    use finite_element_config_mod, only : cellshape_quadrilateral,             &
                                          coord_system_native
    use feign_config_mod,          only : feign_finite_element_config,         &
                                          feign_base_mesh_config,              &
                                          feign_extrusion_config,              &
                                          feign_planet_config

    implicit none

    class(panel_edge_coords_test_type), intent(inout) :: this

    call feign_base_mesh_config(                                               &
        file_prefix='foo',                                                     &
        prime_mesh_name='unit_test',                                           &
        geometry=geometry_spherical,                                           &
        prepartitioned=.false.,                                                &
        topology=topology_fully_periodic,                                      &
        fplane=.false., f_lat_deg=0.0_r_def                                    &
    )
    call feign_extrusion_config(                                               &
        planet_radius=1.0_r_def,                                               &
        domain_height=10.0_r_def,                                              &
        method=method_uniform,                                                 &
        number_of_layers=1_i_def,                                              &
        stretching_method=stretching_method_linear,                            &
        stretching_height=1.0_r_def,                                           &
        eta_values=(/0.5_r_def/)                                               &
    )
    call feign_planet_config(                                                  &
        gravity=9.98_r_def,                                                    &
        omega=7.27E-5_r_def,                                                   &
        rd=300.0_r_def,                                                        &
        cp=1000.0_r_def,                                                       &
        p_zero=100000.0_r_def,                                                 &
        scaling_factor=1.0_r_def                                               &
    )
    call feign_finite_element_config(                                          &
        cellshape=cellshape_quadrilateral,                                     &
        coord_order=0_i_def,                                                   &
        coord_system=coord_system_native,                                      &
        element_order_h=0_i_def,                                               &
        element_order_v=0_i_def,                                               &
        rehabilitate=.true.,                                                   &
        vorticity_in_w1=.false.                                                &
    )

    call init_chi_transforms(geometry_spherical, topology_fully_periodic)

  end subroutine setUp

  !!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
  subroutine tearDown( this )

    use configuration_mod,        only: final_configuration
    use sci_chi_transform_mod,    only: final_chi_transforms

    implicit none

    class(panel_edge_coords_test_type), intent(inout) :: this

    call final_configuration()
    call final_chi_transforms()

  end subroutine tearDown

  !!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
  @Test
  subroutine test_all( this )

    use panel_edge_coords_kernel_mod, only: panel_edge_coords_code
    use, intrinsic :: iso_fortran_env, only : real64

    implicit none

    class(panel_edge_coords_test_type), intent(inout) :: this

    real(kind=r_def),    parameter :: tol = 1.0e-12_r_def
    real(kind=r_def)               :: use_tol

    integer(kind=i_def), parameter :: nlayers = 1
    integer(kind=i_def), parameter :: ndf_wx = 4
    integer(kind=i_def), parameter :: undf_wx = ndf_wx*nlayers
    integer(kind=i_def), parameter :: ndf_pid = 1
    integer(kind=i_def), parameter :: undf_pid = ndf_pid*nlayers

    integer(kind=i_def) :: map_wx(ndf_wx)
    integer(kind=i_def) :: map_pid(ndf_pid)

    real(kind=r_def)    :: alpha_x(undf_wx), beta_x(undf_wx)
    real(kind=r_def)    :: alpha_y(undf_wx), beta_y(undf_wx)
    real(kind=r_def)    :: panel_id(undf_pid)
    real(kind=r_def)    :: chi_1(undf_wx), chi_2(undf_wx), chi_3(undf_wx)
    integer(kind=i_def) :: panel_edge_dist_E(undf_pid), panel_edge_dist_W(undf_pid)
    integer(kind=i_def) :: panel_edge_dist_S(undf_pid), panel_edge_dist_N(undf_pid)

    integer(kind=i_def) :: df, depth

    real(kind=r_def), parameter :: delta_alpha = 0.1_r_def
    real(kind=r_def), parameter :: delta_beta  = 0.1_r_def
    real(kind=r_def), parameter :: alpha0 = pi/4.0_r_def
    real(kind=r_def), parameter :: beta0 = 0.5_r_def
    real(kind=r_def), parameter :: h0 = 1.0_r_def

    integer(kind=i_def), dimension(2) :: east_dofs, west_dofs, south_dofs, north_dofs

    do df = 1, ndf_wx
      map_wx(df) = df
    end do
    map_pid(1) = 1

    ! Test extension from panel 2 to panel 1 in x-direction
    panel_id(1) = 2
    panel_edge_dist_W(1) = 1
    panel_edge_dist_E(1) = -ABS(IMDI)
    panel_edge_dist_N(1) = -ABS(IMDI)
    panel_edge_dist_S(1) = -ABS(IMDI)
    depth = 1

    ! On input the coordinates (alpha, beta, h) are points on the WEST edge of
    ! panel 2 (corresponding to the EAST edge of panel 1)

    ! Flags for Wx dofs on the east edge of the cell: alpha = -pi/4 +  delta_alpha
    east_dofs = (/ 2, 4 /)
    ! Flags for Wx dofs on the west  edge of the cell: alpha = -pi/4
    west_dofs = (/ 1, 3 /)
    ! Flags for Wx dofs on the north edge of the cell: beta0 + delta_beta
    north_dofs = (/ 3, 4 /)
    ! Flags for Wx dofs on the south edge of the cell: beta0
    south_dofs = (/ 1, 2 /)

    do df = 1, 2
      chi_1(west_dofs(df)) = -alpha0
      chi_1(east_dofs(df)) = -alpha0 + delta_alpha
      chi_2(south_dofs(df)) = beta0
      chi_2(north_dofs(df)) = beta0 + delta_beta
    end do

    ! Set height to arbitrary value
    chi_3(:) = h0

    call  panel_edge_coords_code( nlayers,                                &
                                  alpha_x, beta_x,                        &
                                  alpha_y, beta_y,                        &
                                  chi_1, chi_2, chi_3,                    &
                                  panel_id,                               &
                                  panel_edge_dist_W, panel_edge_dist_E,   &
                                  panel_edge_dist_S, panel_edge_dist_N,   &
                                  depth,                                  &
                                  ndf_wx, undf_wx, map_wx,                &
                                  ndf_wx, undf_wx, map_wx,                &
                                  ndf_pid, undf_pid, map_pid              &
                                )
    if ( r_def == real64 ) then
       use_tol = tol
    else
       use_tol = 10.0_r_def*spacing( h0 )
    end if
    ! Beta field should be unchanged
    do df = 1, 2
      @assertEqual(beta0, beta_x(south_dofs(df)), use_tol)
      @assertEqual(beta0 + delta_beta, beta_x(north_dofs(df)), use_tol)
    end do
    ! Alpha field should be (+pi/4, + pi/4 + delta_alpha)
    do df = 1, 2
      @assertEqual(alpha0, alpha_x(west_dofs(df)), use_tol)
      @assertEqual(alpha0 + delta_alpha, alpha_x(east_dofs(df)), use_tol)
    end do

  end subroutine test_all

end module panel_edge_coords_kernel_mod_test
